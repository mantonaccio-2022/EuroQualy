#include "totvs.ch"
#include "FwMvcDef.ch"
#include "tbiConn.ch"
/*
?????????????????????????????????????????????????????????????????????????????
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±????????????????????????????????????????????????????????????????????????»±±
±±?Programa  ³ EQRespCC ?Autor  ³Rodrigo Sousa       ? Data ³  10/09/2013 ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±?Desc.     ³ Programa de inclus?o do cadastro de Responsaveis de Centro ?±±
±±?     	 ³ de Custo.				 								  ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±?Documento ³ 												  			  ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±?Uso       ³ ?±±
±±T????????????????????????????????????????????????????????????????????????±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
?????????????????????????????????????????????????????????????????????????????
*/
User Function EQRespCC()

Private oBrowse := FwMBrowse():New()
oBrowse:SetAlias("SZU")
oBrowse:SetDescription("Cadastro de Responsaveis Centro de Custo")
oBrowse:Activate()

Return
/*
?????????????????????????????????????????????????????????????????????????????
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±????????????????????????????????????????????????????????????????????????»±±
±±?Programa  ³ MenuDef  ?Autor  ³Rodrigo Sousa       ? Data ³  10/09/2013 ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±?Desc.     ³ MenuDef   										  ?±±
±±T????????????????????????????????????????????????????????????????????????±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
?????????????????????????????????????????????????????????????????????????????
*/
Static Function MenuDef()

Local aMenu := {}

ADD OPTION aMenu TITLE 'Pesquisar' 	ACTION 'PesqBrw'			OPERATION 1 ACCESS 0
ADD OPTION aMenu TITLE 'Visualizar' ACTION 'VIEWDEF.EQRespCC'	OPERATION 2 ACCESS 0
ADD OPTION aMenu TITLE 'Incluir' 	ACTION 'VIEWDEF.EQRespCC'	OPERATION 3 ACCESS 0
ADD OPTION aMenu TITLE 'Alterar' 	ACTION 'VIEWDEF.EQRespCC'	OPERATION 4 ACCESS 0
ADD OPTION aMenu TITLE 'Excluir' 	ACTION 'VIEWDEF.EQRespCC'	OPERATION 5 ACCESS 0

Return(aMenu)
/*
?????????????????????????????????????????????????????????????????????????????
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±????????????????????????????????????????????????????????????????????????»±±
±±?Programa  ³ ModelDef ?Autor  ³Rodrigo Sousa       ? Data ³  10/09/2013 ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±?Desc.     ³ ModelDef BeRespCC										  ?±±
±±T????????????????????????????????????????????????????????????????????????±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
?????????????????????????????????????????????????????????????????????????????
*/
Static Function ModelDef()

Local oStruct1 := FwFormStruct(1,"SZU",{|cCampo| EQTrataCpo(1,alltrim(cCampo)) } )
Local oStruct2 := FwFormStruct(1,"SZU",{|cCampo| EQtrataCpo(2,alltrim(cCampo)) } )
Local oModel

oModel := MpFormModel():New("PERESPCC",/*Pre-Validacao*/, {|| .T.},/*Commit*/,/*Cancel*/)
oModel:AddFields("ID_MODEL_ENCH_RESPCC",/*cOwner*/,oStruct1,/*bPreValidacao*/,/*bPosValidacao*/,/*bCarga*/)

oModel:SetPrimaryKey({"ZU_CCUSTO","ZU_CODUSU"})
oModel:SetDescription("Responsaveis Centro de Custo")
oModel:GetModel("ID_MODEL_ENCH_RESPCC"):SetDescription("Responsaveis Centro de Custo")

oModel:AddGrid("ID_MODEL_GRID_RESPCC","ID_MODEL_ENCH_RESPCC",oStruct2,{ |oModelGrid, nLine, cAction, cField| EQPreVldLin(oModelGrid, nLine, cAction, cField)},{ |oModelGrid, nLine| EQPosVldLin(oModelGrid, nLine) })

oModel:GetModel("ID_MODEL_GRID_RESPCC"):SetUniqueLine({"ZU_CODUSU"})
oModel:SetRelation("ID_MODEL_GRID_RESPCC", { {"ZU_FILIAL",'xFilial("SZU")'},{"ZU_CCUSTO","ZU_CCUSTO"} }, SZU->(IndexKey()) )

Return(oModel)
/*
?????????????????????????????????????????????????????????????????????????????
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±????????????????????????????????????????????????????????????????????????»±±
±±?Programa  ³ ViewDef  ?Autor  ³Rodrigo Sousa       ? Data ³  10/09/2013 ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±?Desc.     ³ ViewDef BeRespCC 										  ?±±
±±T????????????????????????????????????????????????????????????????????????±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
?????????????????????????????????????????????????????????????????????????????
*/
Static Function ViewDef()

Local oStruct1 	:= FwFormStruct(2,"SZU",{|cCampo| EQTrataCpo(1,alltrim(cCampo)) } )
Local oStruct2 	:= FwFormStruct(2,"SZU",{|cCampo| EQTrataCpo(2,alltrim(cCampo)) } )
Local oModel 	:= FwLoadModel("EQRespCC")
Local oView		:= FwFormView():New()

oView:SetModel(oModel)
oView:AddField("ID_VIEW_ENCH_RESPCC",oStruct1,"ID_MODEL_ENCH_RESPCC")
oView:AddGrid("ID_VIEW_GRID_RESPCC",oStruct2,"ID_MODEL_GRID_RESPCC")

oView:CreateHorizontalBox("H_TOP",30)
oView:CreateHorizontalBox("H_DOWN",70)

oView:SetOwnerView("ID_VIEW_ENCH_RESPCC","H_TOP")
oView:SetOwnerView("ID_VIEW_GRID_RESPCC","H_DOWN")

//???????????????????????????????????????????????
//³ Habilita Titulo da View 					³
//??????????????????????????????????????????????O
oView:EnableTitleView('ID_VIEW_ENCH_RESPCC','Centro de Custo')
oView:EnableTitleView('ID_VIEW_GRID_RESPCC','Reponsaveis')

Return (oView)
/*
?????????????????????????????????????????????????????????????????????????????
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±????????????????????????????????????????????????????????????????????????»±±
±±?Programa  ³BePosVldLi?Autor  ³Rodrigo Sousa       ? Data ³  10/09/2013 ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±?Desc.     ³ Pss Valida??o da Linha da Grid 							  ?±±
±±?     	 ³ 							 								  ?±±
±±?     	 ³ Parametros:				 								  ?±±
±±?     	 ³ oExp1 = Objeto do Model da Grid							  ?±±
±±?     	 ³ nExp2 = Numero da linha posicionada						  ?±±
±±T????????????????????????????????????????????????????????????????????????±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
?????????????????????????????????????????????????????????????????????????????
*/
Static Function EQPosVldLin(oModelGrid,nLine)

Local lRet			:= .T.
Local oModel 		:= oModelGrid:GetModel()
Local nOperation 	:= oModel:GetOperation()
Local cCCusto		:= oModel:GetValue('ID_MODEL_ENCH_RESPCC','ZU_CCUSTO')
Local cCodResp     	:= oModel:GetValue('ID_MODEL_GRID_RESPCC','ZU_CODUSU')
                                    
If nOperation == MODEL_OPERATION_INSERT
	If !ExistChav("SZU",cCCusto+cCodResp,1,"JAGRAVADO")
		lRet := .F.	
	EndIf
EndIf

Return lRet
/*
?????????????????????????????????????????????????????????????????????????????
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±????????????????????????????????????????????????????????????????????????»±±
±±?Programa  ³BePreVldLi?Autor  ³Rodrigo Sousa       ? Data ³  10/09/2013 ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±?Desc.     ³ Pr? Valida??o da Linha da Grid 							  ?±±
±±?     	 ³ 							 								  ?±±
±±?     	 ³ Parametros:				 								  ?±±
±±?     	 ³ oExp1 = Objeto do Model da Grid							  ?±±
±±?     	 ³ nExp2 = Numero da linha posicionada						  ?±±
±±?     	 ³ nExp3 = A??o												  ?±±
±±?     	 ³ nExp4 = Campo 											  ?±±
±±T????????????????????????????????????????????????????????????????????????±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
?????????????????????????????????????????????????????????????????????????????
*/
Static Function EQPreVldLin(oModelGrid, nLine, cAction, cField)
   
Local lRet 			:= .T.     
Local oModel 		:= oModelGrid:GetModel()
Local nOperation 	:= oModel:GetOperation()  
Local cCodResp		:= oModel:GetValue('ID_MODEL_GRID_RESPCC','ZU_CODUSU')

If cAction = 'CANSETVALUE' 
	If nOperation == MODEL_OPERATION_UPDATE .And. cField == 'ZU_CODUSU' .And. !Empty(cCodResp)
    	lRet := .F.       
    EndIf	
EndIf

Return lRet

/*
?????????????????????????????????????????????????????????????????????????????
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±????????????????????????????????????????????????????????????????????????»±±
±±?Programa  ³BeTrataCpo?Autor  ³Rodrigo Sousa       ? Data ³  10/09/2013 ?±±
±±?????????????????????????????????????????????????????????????????????????±±
±±?Desc.     ³ Programa para defini??o dos campos de Enchoice e Grid	  ?±±
±±?     	 ³ 							 								  ?±±
±±?     	 ³ Parametros:				 								  ?±±
±±?     	 ³ nExp1  = Op??o 1 = Enchoice; 2 = Grid					  ?±±
±±?     	 ³ cExp2  = Nome do Campo									  ?±±
±±T????????????????????????????????????????????????????????????????????????±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
?????????????????????????????????????????????????????????????????????????????
*/
Static Function EQTrataCpo(nOpc,cCampo)

Local lRet := .F.

If nOpc == 1
	lRet := cCampo $ "ZU_CCUSTO|ZU_DESCRCC"
Else	
	lRet := !cCampo $ "ZU_CCUSTO|ZU_DESCRCC"
EndIf	

Return (lRet)